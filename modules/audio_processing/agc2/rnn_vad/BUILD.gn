# Copyright (c) 2018 The WebRTC project authors. All Rights Reserved.
#
# Use of this source code is governed by a BSD-style license
# that can be found in the LICENSE file in the root of the source
# tree. An additional intellectual property rights grant can be found
# in the file PATENTS.  All contributing project authors may
# be found in the AUTHORS file in the root of the source tree.

import("../../../../webrtc.gni")

group("rnn_vad") {
  deps = [
    ":lib",
  ]
}

source_set("lib") {
  sources = [
    "common.h",
    "lp_residual.cc",
    "lp_residual.h",
    "ring_buffer.h",
    "sequence_buffer.h",
    "symmetric_matrix_buffer.h",
  ]
  deps = [
    "../../../../api:array_view",
    "../../../../rtc_base:checks",
  ]
}

if (rtc_include_tests) {
  source_set("lib_test") {
    testonly = true
    sources = [
      "test_utils.cc",
      "test_utils.h",
    ]
    deps = [
      "../../../../api:array_view",
      "../../../../rtc_base:checks",
      "../../../../rtc_base:ptr_util",
      "../../../../test:fileutils",
      "../../../../test:test_support",
    ]
  }

  unittest_resources = [
    "../../../../resources/audio_processing/agc2/rnn_vad/pitch_buf_24k.dat",
    "../../../../resources/audio_processing/agc2/rnn_vad/pitch_lp_res.dat",
  ]

  if (is_ios) {
    bundle_data("unittests_bundle_data") {
      testonly = true
      sources = unittest_resources
      outputs = [
        "{{bundle_resources_dir}}/{{source_file_part}}",
      ]
    }
  }

  rtc_source_set("unittests") {
    testonly = true
    sources = [
      "lp_residual_unittest.cc",
      "ring_buffer_unittest.cc",
      "sequence_buffer_unittest.cc",
      "symmetric_matrix_buffer_unittest.cc",
    ]
    deps = [
      ":lib",
      ":lib_test",
      "../../../../api:array_view",
      "../../../../test:test_support",
    ]
    data = unittest_resources
    if (is_ios) {
      deps += [ ":unittests_bundle_data" ]
    }
  }

  rtc_executable("rnn_vad_tool") {
    testonly = true
    sources = [
      "rnn_vad_tool.cc",
    ]
    deps = [
      ":lib",
      "../../../../api:array_view",
      "../../../../common_audio:common_audio",
      "../../../../rtc_base:rtc_base_approved",
      "../../../../test:test_support",
    ]
  }
}
