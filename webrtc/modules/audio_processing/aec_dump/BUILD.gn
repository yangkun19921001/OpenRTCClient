import("../../../webrtc.gni")  # This contains def of 'rtc_enable_protobuf'

rtc_source_set("aec_dump") {
  sources = [
    "aec_dump_factory.h",
  ]

  public_deps = [
    "..:aec_dump_interface",
  ]

  deps = [
    "../../../base:rtc_base_approved",
  ]
}

if (rtc_enable_protobuf) {
  rtc_source_set("aec_dump_impl") {
    sources = [
      "aec_dump_impl.cc",
      "aec_dump_impl.h",
      "capture_stream_info.cc",
      "capture_stream_info.h",
      "write_to_file_task.cc",
      "write_to_file_task.h",
    ]

    public = []

    public_deps = [
      ":aec_dump",
      "..:aec_dump_interface",
    ]

    deps = [
      "../../../base:protobuf_utils",
      "../../../base:rtc_base_approved",
      "../../../base:rtc_task_queue",
      "../../../modules:module_api",
      "../../../system_wrappers",
    ]

    deps += [ "../:audioproc_debug_proto" ]
  }

  rtc_source_set("aec_dump_unittests") {
    testonly = true
    defines = []
    deps = [
      ":aec_dump_impl",
      "..:aec_dump_interface",
      "..:audioproc_debug_proto",
      "../../../base:rtc_task_queue",
      "../../../modules:module_api",
      "../../../test:test_support",
      "//testing/gtest",
    ]
    sources = [
      "aec_dump_unittest.cc",
    ]
  }
}

rtc_source_set("null_aec_dump_factory") {
  assert_no_deps = [ ":aec_dump_impl" ]
  sources = [
    "null_aec_dump_factory.cc",
  ]

  public_deps = [
    ":aec_dump",
    "..:aec_dump_interface",
  ]
}
